// Separates the test task into unit and cucumber test tasks

test {
    exclude '**'
}

task unitTest (type: Test) {
    systemProperty "spring.profiles.active", "dev"

    exclude '**/cucumber/**'
}

task cucumberTest (type: Test) {
    systemProperty "spring.profiles.active", "dev"
    systemProperty "cucumber.options", "--tags @dev"

    include '**/cucumber/**'
}

task cucumberProdTest (type: Test) {
    systemProperty "spring.profiles.active", "prod"
    systemProperty "cucumber.options", "--tags @prod"

    include '**/cucumber/**'
}

test.dependsOn unitTest

// If running in Travis, we want build to run the dev Cucumber tests as well
if(System.env.TRAVIS == 'true') {
  test.dependsOn cucumberTest
}
// If running in Travis and it is not a pull request, we want to run the prod Cucumber tests as well
if(System.env.TRAVIS_PULL_REQUEST == 'false') {
  test.dependsOn cucumberProdTest
}
cucumberProdTest.mustRunAfter cucumberTest
cucumberTest.mustRunAfter unitTest